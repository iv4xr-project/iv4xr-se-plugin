plugins {
    id 'org.jetbrains.kotlin.multiplatform' version "$kotlin_version"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id("org.jetbrains.dokka") version "$kotlin_version"
    id 'maven-publish'
    id 'jacoco'
}

group = 'com.github.iv4xr-project'
version = '0.9.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://repo.maven.apache.org/maven2" }
}

kotlin {
    jvm {
        withJava()
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.8'
                javaParameters = true
            }

        }
        testRuns["test"].executionTask.configure {
            useJUnit()
            useJUnitPlatform()
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version")
                implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlin_serialization_version")
                implementation("io.ktor:ktor-client-resources:$ktor_version")
                implementation("io.ktor:ktor-client-core:$ktor_version")
                implementation("io.ktor:ktor-client-cio:$ktor_version")
                implementation("io.ktor:ktor-client-content-negotiation:$ktor_version")
                implementation("io.ktor:ktor-client-auth:$ktor_version")
                implementation("io.ktor:ktor-serialization-kotlinx-json:$ktor_version")

            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation("io.ktor:ktor-client-okhttp:$ktor_version")
                implementation "com.github.iv4xr-project:aplib:$aplib_version"
                implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-junit5'
                implementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'
            }
        }
    }
}

tasks.jacocoTestReport {

    sourceDirectories.setFrom(files(project.projectDir))
    executionData.setFrom(
            fileTree(project.projectDir) {
                setIncludes(["**/**/*.exec", "**/**/*.ec"])
            }
    )

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport
