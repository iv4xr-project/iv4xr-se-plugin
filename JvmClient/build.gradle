plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.6.21'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.21'
    id("org.jetbrains.dokka") version "1.6.21"
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'maven-publish'
    id "se.thinkcode.cucumber-runner" version "0.0.11"
}

group = 'com.github.iv4xr-project'
version = '0.7.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://repo.maven.apache.org/maven2" }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

kotlin {
    jvm {
        withJava()
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.8'
                javaParameters = true
            }

        }
        testRuns["test"].executionTask.configure {
            useJUnit()
            useJUnitPlatform()
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0")
                implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2")
                implementation("io.ktor:ktor-client-resources:$ktor_version")
                implementation("io.ktor:ktor-client-core:$ktor_version")
                implementation("io.ktor:ktor-client-cio:$ktor_version")
                implementation("io.ktor:ktor-client-content-negotiation:$ktor_version")
                implementation("io.ktor:ktor-client-auth:$ktor_version")
                implementation("io.ktor:ktor-serialization-kotlinx-json:$ktor_version")

            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation("io.ktor:ktor-client-okhttp:$ktor_version")
                implementation 'com.github.iv4xr-project:aplib:1.4.2'
                implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
                implementation "org.jetbrains.kotlin:kotlin-reflect:1.6.10"
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-junit5'
                implementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.2'
                implementation group: 'io.cucumber', name: 'cucumber-java', version: '7.2.3'
                implementation group: 'io.cucumber', name: 'cucumber-junit', version: '7.2.3'
            }
        }
    }
}

// Output to build/libs/shadow.jar
shadowJar{
    archiveBaseName.set('se-shadow')
    archiveClassifier.set('')
    archiveVersion.set(version)
}

cucumber {
    glue = 'bdd'
    plugin = ['pretty']
    tags = ''
    name = ''


    featurePath = 'src/jvmTest/resources/features/se-testing/astronaut'
    main = 'io.cucumber.core.cli.Main'
}
